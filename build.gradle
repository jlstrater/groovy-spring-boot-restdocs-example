plugins {
    id "application"
    id "codenarc"
    id "groovy"
    id "com.gradle.build-scan" version "2.0.2"
    id "org.asciidoctor.convert" version "1.5.9.2"
    id "org.springframework.boot" version "2.0.1.RELEASE"
}

mainClassName = "sample.ExampleGreetingsApplication"

repositories {
    maven {
        url 'https://repo.spring.io/libs-snapshot'
    }
}

dependencies {
    implementation(platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion"))

    asciidoctor "org.springframework.restdocs:spring-restdocs-asciidoctor:${springRestDocsVersion}"

    compile "org.codehaus.groovy:groovy-all:2.5.0"
    compile "org.springframework.boot:spring-boot-starter-data-mongodb-reactive"
    compile "org.springframework.boot:spring-boot-starter-webflux"

    testCompile "io.projectreactor:reactor-test"
    testCompile "org.spockframework:spock-core:${spockVersion}"
    testCompile "org.spockframework:spock-spring:${spockVersion}"
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "org.springframework.restdocs:spring-restdocs-core:${springRestDocsVersion}"
    testCompile "org.springframework.restdocs:spring-restdocs-webtestclient:${springRestDocsVersion}"
}

buildScan {
    termsOfServiceUrl = "https://gradle.com/terms-of-service"
    termsOfServiceAgree = "yes"
    publishAlways()
}

if (JavaVersion.current().isJava9Compatible()) {
    def jaxbJvmArgs = ['--add-modules', 'java.xml.bind']
    tasks.withType(GroovyCompile) {
        groovyOptions.fork = true
        groovyOptions.forkOptions.jvmArgs.addAll(jaxbJvmArgs)
    }
    tasks.withType(Test) {
        jvmArgs jaxbJvmArgs
    }
}

apply from: 'gradle/custom.gradle'

